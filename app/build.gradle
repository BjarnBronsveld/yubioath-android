apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--dirty=-dirty'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getVersionCode = { ->
    def m = (getVersionName() =~ /(\d+)\.(\d+)\.(\d)+(-(\d+)-)?(-dirty)?/)[0]
    return (m[1] as int) * 10000000 + (m[2] as int) * 10000 + (m[3] as int) * 1000 + ((m[5] ?: 0) as int)
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    defaultConfig {
        applicationId "com.yubico.yubioath"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode getVersionCode()
        versionName getVersionName()
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    testImplementation 'org.robolectric:robolectric:3.5.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.11.0'

    implementation 'commons-codec:commons-codec:1.11'
    implementation group: 'com.fidesmo', name: 'nordpol-android', version: '0.1.22', ext: 'aar', transitive: true
    implementation 'com.journeyapps:zxing-android-embedded:3.4.0'
    implementation 'com.pixplicity.sharp:library:1.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19.3'
    implementation 'org.jetbrains.anko:anko-common:0.10.3'
    implementation 'org.jetbrains.anko:anko-sdk15:0.10.3'
    implementation "org.jetbrains.anko:anko-coroutines:0.10.3"

    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:preference-v14:27.0.2'

    implementation "android.arch.lifecycle:extensions:1.0.0"
}
repositories {
    mavenCentral()
}
kotlin {
    experimental {
        coroutines "enable"
    }
}
